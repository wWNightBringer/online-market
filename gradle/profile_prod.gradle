
configurations {
  all {
    resolutionStrategy {
      force "org.hibernate.orm:hibernate-core:${hibernateVersion}"
    }
  }
}

dependencies {
    implementation "org.postgresql:postgresql"
    testImplementation "org.testcontainers:postgresql"
}

ext {
    springProfiles = "prod" + springProfiles

    if (project.hasProperty("api-docs")) {
        springProfiles += ",api-docs"
    }
}

springBoot {
    buildInfo()
}

bootRun {
    args = ["--spring.profiles.active=${springProfiles}"]
}


processResources {
    inputs.property('version', version)
    inputs.property('springProfiles', springProfiles)
    filesMatching("**/application.yml") {
        filter {
            it.replace("#project.version#", version)
        }
    }
    filesMatching("**/bootstrap.yml") {
        filter {
            it.replace("#spring.profiles.active#", springProfiles)
        }
    }
}

task integrationTest(type: Test) {
    maxHeapSize = "1G"
    useJUnitPlatform()
    description = "Execute integration tests."
    group = "verification"
    include "**/*IT*", "**/*IntTest*"
    testLogging {
        events 'FAILED', 'SKIPPED'
    }
    systemProperty('spring.profiles.active', 'testprod')
    systemProperty('java.security.egd', 'file:/dev/./urandom')
    // uncomment if the tests reports are not generated
    // ignoreFailures true
    reports.html.required = false
}
integrationTest.dependsOn test


